plugins {
    id 'org.springframework.boot' version '3.0.1'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id "com.diffplug.spotless" version "6.11.0"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group = 'app.nftguy'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'build/swagger-code-cardanowallet/build/libs', include: ['*.jar'])
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'com.bloxbean.cardano:cardano-client-lib:0.3.0'
    implementation 'com.bloxbean.cardano:cardano-client-backend-blockfrost:0.3.0'
    implementation 'org.json:json:20220320'
    implementation 'org.jsoup:jsoup:1.15.3'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation 'javax.mail:javax.mail-api:1.6.2'

    //test dependencies
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        //fix conflict with org.json:json
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }

    //cardano wallet swagger dependencies
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.35'  // Swagger Codegen V3
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'
    implementation 'org.netbeans.external:com-google-gson:RELEASE113'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation "com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4"
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        // Use the default importOrder configuration
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()  // fixes formatting of type annotation
    }
}

swaggerSources {
    cardanowallet {
        inputFile = file('cardano-wallet-openapi/cardano-wallet-openapi.yaml')
        code {
            language = 'java'
            configFile = file('cardano-wallet-openapi/cardano-wallet-openapi-config.json')
            templateDir = file('cardano-wallet-openapi/template')
        }
    }
}

tasks.named("bootBuildImage") {
    docker {
        publishRegistry {
            url = "ghcr.io"
            token = "$System.env.GHCR_TOKEN"
            imageName = "ghcr.io/ablewitt/nftapi:${version}"
        }
    }
}



